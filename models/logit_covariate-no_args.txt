# Input:
#   T: number of tables
#   P (= 1): numer of covariate variables
#   R, C: initial and final aggregated categories
#   Y.1[T, R], Y.2[T, C]: initial and final aggregated matrices
#   N.1[T], N.2[T]: initial and final aggregated counts
#   d[R, C]: intercept prior
#   l: precision related prior
# Output:
#   theta.2[T, C]: final estimated aggregated matrix as fractions
#   delta[R, C]: intercept
#   beta[T, R, C]: estimated transition matrix

data {
  for (t in 1:T) {
    for (r in 1:R) {
      f.1[t, r] <- Y.1[t, r] / N.1[t]
    }
  }
}
model {
  for (t in 1:T) {
    Y.2[t,] ~ dmulti(theta.2[t, 1:C], N.2[t])
    for (c in 1:C) {
      theta.2[t, c] <- inprod(beta[t, 1:R, c], f.1[t, 1:R])
    }
    # Undo multinomial logit
    for (r in 1:R) {
      for (c in 1:C) {
        beta.aux[t, r, c] <- exp(delta[r,c])
      }
      beta[t, r, 1:C] ~ ddirch(lambda * beta.aux[t, r, 1:C] / sum(beta.aux[t, r, 1:C]))
    }
  }
  for (r in 1:R) {
    # Prior in inital/original space
    delta.init[r, 1:C] ~ ddirch(d[r, 1:C])
    # Apply multinomial logit
    for (c in 1:C) {
      delta.aux[r, c] <- log(delta.init[r, c])
      delta[r, c] <- delta.aux[r, c] - mean(delta.aux[r, 1:C])
    }
  }
  lambda ~ dexp(l)
}
