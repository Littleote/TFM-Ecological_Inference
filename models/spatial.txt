
model {
  for (t in 1:T) {
    ## 1st Stage Likelihood (for the observed data)
    f.2[t, 2] ~ dnorm(theta.2[t], tau.theta[t])
    theta.2[t] <- beta[t, 1, 2] * f.1[t, 1] + beta[t, 2, 2] * f.1[t, 2]
    tau.theta[t] <- N[t] / (
      (beta[t, 1, 1] * beta[t, 1, 2] * f.1[t, 1]) +
      (beta[t, 2, 1] * beta[t, 2, 2] * f.1[t, 2])
    )

    for (r in 1:2) {
      beta.aux[t, r] <- exp(delta[r] + upsilon[r, t] + nu[r, t])
      beta[t, r, 2] <- beta.aux[t, r] / (1 + beta.aux[t, r])
      beta[t, r, 1] <- 1 - beta[t, r, 2]
      
      ## Unstructured random effects
      nu[r, t] ~ dnorm(0, tau.v[r])
    }
    upsilon[1, t] <- 0
  }
  
  ## ICAR distribution for spatially structured random effects
  upsilon[2, 1:T] ~ car.normal(ADJ.adj[], ADJ.weights[], ADJ.num[], tau.u[2])
  
  ## Third stage priors
  tau.u[1] <- 0
  tau.u[2] ~ dgamma(a.u[2], r.u[2])
  for (r in 1:2) {
    delta[r] ~ dlogis(0, 1)
    tau.v[r] ~ dgamma(a.v[r], r.v[r])
  }
}
