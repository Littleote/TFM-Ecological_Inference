# Input:
#   T: number of tables
#   P (= 0): numer of covariate variables
#   R, C: initial and final aggregated categories
#   Y.2[T, C]: final aggregated matrices
#   f.1[T, R]: initial aggregated matrices as fractions
#   N[T]: aggregated counts
#   l: precision related prior
# Output:
#   theta.2[T, C]: final estimated aggregated matrices as fractions
#   delta[R, C]: intercept in logit space
#   beta[T, R, C]: estimated transition matrix (by value)
#   lambda: precision related value for beta

model {
  for (t in 1:T) {
    Y.2[t, 1:C] ~ dmulti(theta.2[t, 1:C], N[t])
    for (c in 1:C) {
      theta.2[t, c] <- inprod(beta[t, 1:R, c], f.1[t, 1:R])
    }
    for (r in 1:R) {
      for (c in 1:C) {
        beta.aux[t, r, c] <- exp(delta[r, c])
      }
      beta[t, r, 1:C] ~ ddirch(lambda * beta.aux[t, r, 1:C])
    }
  }
  for (r in 1:R) {
    for (c in 1:(C-1)) {
      delta.aux[r, c] ~ dnorm(0, 1e-6)
      delta[r, c] <- delta.aux[r, c]
    }
    delta[r, C] <- 0
  }
  lambda ~ dexp(l)
}